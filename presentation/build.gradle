apply plugin: 'livetrack-library.android'

android {
    defaultConfig {
        manifestPlaceholders = [mapsApiKey: getGoogleMapApiKey()]
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation project(Modules.domainApplicationImplementations)

    implementation Libs.kotlinStdLib
    implementation Libs.kotlinCoroutines

    implementation Libs.appCompat
    implementation Libs.material
    implementation Libs.constraintLayout
    implementation Libs.lifecycle

    implementation Libs.dagger
    implementation Libs.daggerAndroid
    kapt Libs.daggerProccessor
    kapt Libs.daggerAndroidProccessor

    implementation Libs.navigationFragment
    implementation Libs.navigationUi

    implementation Libs.maps

    implementation Libs.easyPermissions
}

private String getGoogleMapApiKey() {
    def env = System.getenv('googleMap_apiKey')
    if (env != null) {
        return env
    }
    def localProperties = rootProject.file('local.properties')
    if (localProperties.exists()) {
        Properties properties = new Properties()
        properties.load(rootProject.file('local.properties').newDataInputStream())

        def googleMapApiKey = properties.getProperty('googleMap.apiKey')
        if (googleMapApiKey != null) {
            return googleMapApiKey
        }
    }

    throw new GradleException("googleMapKey not found")
}
